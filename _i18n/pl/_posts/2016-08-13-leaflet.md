---
layout: post
title: Leaflet - Wstęp do map
description: Open-source biblioteka JavaScript do obsługi interaktywnych map
headline: Premature optimization is the root of all evil.
categories: [programowanie]
tags: [JavaScript, GIS]
lang: pl
---

Jestem z powrotem! Tym razem chciałabym pokazać Wam trochę map. Dzisiejszym tematem będzie: Jak w prosty sposób można obsłużyć mapy na własnej stronie internetowej. Do tego potrzebne nam będzie:

1. [Leaflet](https://leafletjs.com/) - biblioteka JavaScript do obsługi map
2. Fragmenty mapy (Maps tiles)
3. Ruby on Rails (nie jest wymagany, na początek wystarczy strona **html**)

Zacznijmy od początku. Co to są **map tiles**? Prostymi słowami, są to małe kwadratowe obrazki zawierające wygląd naszej mapy. Są one przygotowywane w postaci paczek, by można było na stronie wyświetlić większy fragment mapy.

## Skąd możemy dostać takie fragmenty mapy?

Można zarejestrować się na stronie [Mapbox](https://www.mapbox.com/) i skorzystać z ich darmowego pakietu do obsługi map lub użyć [OpenStreetMap](https://openstreetmap.org). Jest wiele serwisów, które dostarczają map tiles, na końcu tego artykułu postaram się wymienić kilka z nich.

## Przygotowania

Gdy w naszym projekcie mamy już dołączoną bibliotekę **Leaflet** i wybraliśmy z jakich talii będziemy korzystać możemy zacząć.

W naszym pliku `index.html` dodajemy następująca linijkę:

```html
<div id='map'></div>
```

Po stronie JavaScript potrzebujemy jeszcze ustawić wybranego dostawcę map tiles. W tym przykładzie będę używać CoffeeScript ale każda z prezentowanych funkcjonalności jest też możliwa do osiągnięcia w czystym JavaScript.

```coffeescript
jQuery ->
  map = L.map('map').setView([50.301, 18.654], 13)
```

#### Mapbox tiles

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/mapbox.png"><img src="{{ site.baseurl_root }}/images/leaflet/mapbox.png"></a>
</figure>

```coffeescript
  L.tileLayer('https://api.tiles.mapbox.com/v4/{id}/{z}/{x}/{y}.png?access_token={accessToken}', {
      attribution: 'Map data &copy; <a href="http://openstreetmap.org">OpenStreetMap</a> contributors, <a href="http://creativecommons.org/licenses/by-sa/2.0/">CC-BY-SA</a>, Imagery © <a href="http://mapbox.com">Mapbox</a>',
      maxZoom: 18,
      id: 'your id',
      accessToken: 'your token'
  }).addTo(map)
```

#### OpenStreetMap tiles

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/osm.png"><img src="{{ site.baseurl_root }}/images/leaflet/osm.png"></a>
</figure>

```coffeescript
  L.tileLayer('http://{s}.tile.osm.org/{z}/{x}/{y}.png', {
      attribution: 'Map data &copy; <a href="http://openstreetmap.org">OpenStreetMap</a> contributors, <a href="http://creativecommons.org/licenses/by-sa/2.0/">CC-BY-SA</a>',
      maxZoom: 18
  }).addTo(map)
```

#### Toner tiles

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/toner.png"><img src="{{ site.baseurl_root }}/images/leaflet/toner.png"></a>
</figure>

```coffeescript
  L.tileLayer('http://tile.stamen.com/toner/{z}/{x}/{y}.png', {
      attribution: 'Map tiles by <a href="http://stamen.com">Stamen Design</a>, under <a href="http://creativecommons.org/licenses/by/3.0">CC BY 3.0</a>. Data by <a href="http://openstreetmap.org">OpenStreetMap</a>, under <a href="http://www.openstreetmap.org/copyright">ODbL</a>.',
      maxZoom: 18
  }).addTo(map)
```

#### WaterColor tiles

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/watercolor.png"><img src="{{ site.baseurl_root }}/images/leaflet/watercolor.png"></a>
</figure>

```coffeescript
  L.tileLayer('http://tile.stamen.com/watercolor/{z}/{x}/{y}.png', {
      attribution: 'Map tiles by <a href="http://stamen.com">Stamen Design</a>, under <a href="http://creativecommons.org/licenses/by/3.0">CC BY 3.0</a>. Data by <a href="http://openstreetmap.org">OpenStreetMap</a>, under <a href="http://creativecommons.org/licenses/by-sa/3.0">CC BY SA</a>.',
      maxZoom: 18
  }).addTo(map)
```

I to wszystko, po odświeżeniu przeglądarki powinniśmy widzieć już mapę.

## Dodawanie obiektów na mapę

Teraz zaczyna się prawdziwa zabawa! Możemy zacząć dodawać różne obiekty na mapę. Jedną ważną rzecz watro tu zaznaczyć. Każdy obiekt, który będzie pojawiał się na mapie posiada swoje współrzędne (tak jak w układnie współrzędnych na Matematyce). Leaflet używa dwóch układów współrzędnych. Układu **Kartezjańskiego** i układu typu **Szerokość-długość geograficzna**. Pierwszy z nich podaje współrzędne punktu jako `[długość, szerokość]` a drugi jako `[szerokość, długość]`. Zapamiętanie, gdzie którego z tych systemów używamy jest bardzo proste. Dla obiektu `GeoJSON` używamy układu **Kartezjańskiego** dla pozostałych elementów układu **Geograficznego**.

#### Marker

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/marker.png"><img src="{{ site.baseurl_root }}/images/leaflet/marker.png"></a>
</figure>

```coffeescript
marker = L.marker([50.28895538456755, 18.681907653808594]).addTo(map)
```

#### Custom marker

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/custom-marker.png"><img src="{{ site.baseurl_root }}/images/leaflet/custom-marker.png"></a>
</figure>

```coffeescript
customIcon = L.icon(
  iconUrl: 'assets/womanonrails.jpg'
  iconSize:     [50, 50]
  iconAnchor:   [25, 25]
  className: 'round'
)

customMarker = L.marker(
  [50.29391802001304, 18.665471076965332],
  icon: customIcon
).addTo(map)
```

#### HTML marker

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/html-marker.png"><img src="{{ site.baseurl_root }}/images/leaflet/html-marker.png"></a>
</figure>

```coffeescript
divIcon = L.divIcon(
  className: 'div-icon'
  html: '<div>This is my custom html</div>'
  iconSize: [100, 20]
)

L.marker([50.2845132498121, 18.668603897094727], icon: divIcon).addTo(map)
```

#### Okrąg

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/circle.png"><img src="{{ site.baseurl_root }}/images/leaflet/circle.png"></a>
</figure>

```coffeescript
circle = L.circle(
  [50.290024725454515, 18.640880584716797], 500,
  color: 'red',
  fillColor: '#f03',
  fillOpacity: 0.5
).addTo(map)
```

#### Wielokąt

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/poligon.png"><img src="{{ site.baseurl_root }}/images/leaflet/poligon.png"></a>
</figure>

```coffeescript
polygon = L.polygon([
  [50.29830444909283, 18.61384391784668],
  [50.29619353058144, 18.620710372924805],
  [50.29084727901576, 18.616890907287598],
  [50.29125855046426, 18.61354351043701],
  [50.2941373510724,  18.611998558044434],
  [50.29830444909283, 18.61384391784668]
]).addTo(map)
```

#### Popups

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/popup.png"><img src="{{ site.baseurl_root }}/images/leaflet/popup.png"></a>
</figure>

```coffeescript
marker.bindPopup("<b>Hello world!</b><br>I am a popup.").openPopup()
circle.bindPopup("I am a circle.")
polygon.bindPopup("I am a polygon.")

```

```coffeescript
popup = L.popup()
  .setLatLng([51.5, -0.09])
  .setContent("I am a standalone popup.")
  .openOn(map)
```

#### Zdarzenia

<figure>
  <a href="{{ site.baseurl_root }}/images/leaflet/event.png"><img src="{{ site.baseurl_root }}/images/leaflet/event.png"></a>
</figure>

```coffeescript
onMapClick = (e) -> console.log "Coordinate: #{e.latlng}"
map.on('click', onMapClick)
```

```coffeescript
onMapClick = (e) ->
  popup = L.popup()
  popup.setLatLng(e.latlng)
  popup.setContent("You clicked the map at #{e.latlng}")
  popup.openOn(map);
  console.log "Coordinate: #{e.latlng}"

map.on('click', onMapClick)
```

#### GeoJSON

Myślę, że `geoJson` jest jedną z najbardziej wszechstronnych opcji biblioteki Leaflet. Dzięki niej można wyświetlać wszystkie dotychczas poznane obiekty. Dodatkowo można w łatwy sposób zmienić style obiektów, podpinać do nich zdarzenia a także wyświetlać na mapie dane **GIS** bezpośrednio z bazy danych takiej jak [PostgreSQL](https://www.postgresql.org/).

```coffeescript
geoData = {
  "type": "Feature",
  "properties": {},
  "geometry": {
    "type": "Polygon",
    "coordinates": [[
      [18.613758087158203, 50.298386690775416],
      [18.618135452270508, 50.3022518894775],
      [18.619508743286133, 50.302279301112605],
      [18.62457275390625, 50.30570563109659],
      [18.62959384918213, 50.30510261491786],
      [18.624873161315918, 50.3016488295082],
      [18.627877235412598, 50.3005797498622],
      [18.622384071350098, 50.29600162425392],
      [18.613758087158203, 50.298386690775416]
    ]]
  }
}

style = {
  "color": "#ff7800",
  "weight": 5,
  "opacity": 0.65
}

L.geoJson(geoData, style: style).addTo(map)
```

## Na koniec

To co pokazałam to tylko mały wycinek tego co można zrobić dzięki bibliotece **Leaflet**. Jeżeli szukasz bardziej zaawansowanych funkcji (jak na przykład rysowanie kształtów) zapraszam do zapoznania się z poniższymi linkami:

- [Leaflet](https://leafletjs.com/)
- [Leaflet Draw](https://github.com/Leaflet/Leaflet.draw)
- [GeoJSON](http://geojson.io/)
- [Leaflet Routing Machine](http://www.liedman.net/leaflet-routing-machine/)
- [OpenLayers](http://openlayers.org/)
- [Google Maps Javascript API](https://developers.google.com/maps/documentation/javascript/?csw=1)
- [Modest Maps](https://github.com/modestmaps/modestmaps-js)
- [OpenStreetMap](https://openstreetmap.org)
- [Bing maps](https://www.microsoft.com/maps/)
- [Google Maps](https://www.google.pl/maps?source=tldso)
- [Mapbox](https://www.mapbox.com/)
- [MapTiler](http://www.maptiler.com/)
- [Maps Stamen](http://maps.stamen.com/)
- [Map Quest](https://www.mapquest.com/)
- [Tiles](https://wiki.openstreetmap.org/wiki/Tiles)
